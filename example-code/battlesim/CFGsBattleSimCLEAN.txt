Start               : Includes Defines TypeDcls FunctionDcls Program FunctionDcls
Includes            : Include Includes
                    |
Include             : "Include (" HeaderId ")"    // Header-filer indeholder TypeDcls og FunctionDcls
Defines             : Define Defines
                    |
Define              : "Define " Id " " DefineType
DefineType          : Number
                    | String
                    | Boolean
TypeDcls            : TypeDcl TypeDcls
                    |
TypeDcl             : "Type " Id MemberDcl MemberDcls FunctionDcls " End Type"
MemberDcls          : MemberDcl MemberDcls
                    |
MemberDcl           : Id " as " TypeId
FunctionDcls        : FunctionDcl FunctionDcls
                    |
FunctionDcl         : "Function " TypeId Id "(" Param ")" Stmts " End Function"
Param               : MemberDcl Params
Params              : ", " Param
                    |
Program             : "Begin Program " Stmts " End Program"
Stmts               : Stmt Stmts
                    |
Stmt                : Dcl
                    | Assignment
                    | IfStmt
                    | LoopStmt
                    | SwitchStmt
                    | "Return " ReturnV
ReturnV             : Variable
                    |
IfStmt              : "If " Arithmetic " Then " Stmts ElifStmt
ElifStmt            : " End If"
                    | " Else " Stmts " End If"
                    | " Else " IfStmt
SwitchStmt          : "Switch " Variable SwitchCases SwitchDefault " End Switch'"
SwitchCases         : SwitchCase SwitchCases
                    |
SwitchCase          : "Case " Variable Stmts
SwitchDefault       : "Default " Stmts
                    |
Arithmetic          : "(" Arithmetic ")"
                    | Variable A
                    | LeftUnOperator Variable
A                   : BinOperator Variable
                    | UnOperator
Assignment          : NestedId AssignOperator RightAssign
                    | NestedId "(" Id MultiAssign Variable ")"
MultiAssign         : ", " Id MultiAssign Variable ", "
                    | ") = ("
AssignOperator      : " = "
                    | " += "
                    | " -= "
                    | " %= "
                    | " *= "
                    | " /= "
RightAssign         : Arithmetic
                    | Variable
                    | FunctionCall
FunctionCall        : NestedId "(" Arguments ")"
                    | NestedId "()"
Arguments           : Variable ", " Arguments
                    | Arithmetic ", " Arguments
                    | Variable
                    | Arithmetic
LoopStmt            : WhileStmt
                    | ForStmt
                    | ForeachStmt
WhileStmt           : "While " Arithmetic " Do " Stmts " End While"
ForeachStmt         : "Foreach " TypeId " " Id " In " NestedId " Do " Stmts " End Foreach"
ForStmt             : "For " Integer ForIterator Integer " Do " Stmts " End For"
ForIterator         : " To "
                    | " DownTo "
BinOperator         : " * "
                    | " + "
                    | " / "
                    | " - "
                    | " % "
                    | LogicOperator
                    | RelaOperator
LogicOperator       : " AND "
                    | " OR "
RelaOperator        : " EQUALS "
                    | " GREATERTHAN "
                    | " LESSTHAN "
                    | " GREATERTHANEQUALS "
                    | " LESSERTHANEQUALS "
UnOperator          : "++"
                    | "--"
LeftUnOperator      : " NOT"
                    | " -"
                    | UnOperator
Variable            : NestedId
                    | Number
                    | String
                    | Boolean
Dcl                 : "Declare" Id Dcls " as " TypeId    // Declare units as Unit[90]
Dcls                : ", " Id Dcls
                    |
TypeId              : Id
                    | "Number"
                    | "String"
                    | "Boolean"
                    | "Void"
                    | "List<" TypeId ">"
                    | TypeId "[" Integer "]"
NestedId            : Id Nest
Nest                : "." NestedId
                    | "[" Integer "]" N
                    |
Id                  : "id"
HeaderId            : "hid"
Number              : "num"
String              : "str"
Boolean             : "bool"
Integer             : "int"

////////////////////////////////////////////////////////////////////////////////
/* Regular Expressions - testet på https://regex101.com/   // Nedenstående er keywords i sproget
 * Id                  : ^(?=[a-zA-Z_]+[a-zA-Z0-9_]*)(?!.*(AND|OR|NOT|EQUALS|GREATERTHAN|GREATERTHANEQUALS|LESSTHAN|LESSTHANEQUALS|If|While|Do|Foreach|Function|End|Begin|Program|For|Type|as|Number|String|Boolean|Declare|In|Return||Else||List|Include|Switch|Default|Case|NULL|Define|Void)).+$
 * HeaderId            : ^.*\.(bs)$
 * Number              : ^[+-]?[0-9]*\.?[0-9]+$
 * String              : ^"([^"\\]*(\\.[^"\\]*)*)"$
 * Boolean             : ^(true|false)$
 * Integer             : ^[0-9]+$
 * RegEx'en virker ikke med https://services.brics.dk/java/grammar/demo.html
 * hvor der er testet for ambiguity.                                          */
////////////////////////////////////////////////////////////////////////////////
