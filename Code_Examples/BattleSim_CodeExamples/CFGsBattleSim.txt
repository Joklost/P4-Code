
/*
 * End of line bør repræsenteres på en eller anden måde
 */
////////////////////////////////////////////////////////////////////////////////
Start               : Includes Defines TypeDcls FunctionDcls Program FunctionDcls
Includes            : Include Includes
                    |
Include             : "Include (" String ")"    // Header-filer indeholder TypeDcls og FunctionDcls
Defines             : Define Defines
                    |
Define              : "Define " Id " " DefineType
DefineType          : Number
                    | String
                    | Boolean
TypeDcls            : TypeDcl TypeDcls
                    |
TypeDcl             : "Type " Id MemberDcl MemberDcls FunctionDcls " End Type"
MemberDcls          : MemberDcl MemberDcls
                    |
MemberDcl           : Id " as " TypeId
FunctionDcls        : FunctionDcl FunctionDcls
                    |
FunctionDcl         : "Function " TypeId Id "(" Param ")" Stmts " End Function"  //******* ÆNDRET
Param               : MemberDcl Params                                           //******* ÆNDRET
                    |                                                            //******* ÆNDRET
Params              : ", " Param                                                 //******* ÆNDRET
                    |                       // FOLLOW - ) ??                     //******* ÆNDRET
Program             : "Begin Program " Stmts " End Program"
////////////////////////////////////////////////////////////////////////////////
Stmts               : Stmt Stmts
                    |
Stmt                : Dcl
                    | Assignment
                    | IfStmt
                    | LoopStmt
                    | SwitchStmt
                    | "Return " ReturnV                                         //******** Ændret
ReturnV             : Variable                                                  //******** Ændret
                    | // follow set????                                                          //******** Ændret
/* *** potential horizontal ambiguity: Stmts[#1]: Stmt <--> Stmts */
////////////////////////////////////////////////////////////////////////////////
IfStmt                : "If " Arithmetic " Then " Stmts ElifStmt                //******** Ændret
ElifStmt              : " End If"                                               //******** Ændret
                      | " Else " Stmts " End If"                                //******** Ændret
/* *** potential horizontal ambiguity: IfStmt[#1]: "If " Arithmetic " Then " Stmts <--> V1 */
////////////////////////////////////////////////////////////////////////////////
SwitchStmt          : "Switch " Variable SwitchCases SwitchDefault " End Switch'"
SwitchCases         : SwitchCase SwitchCases
                    |
SwitchCase          : "Case " Variable Stmts
SwitchDefault       : "Default " Stmts
                    |
/* *** potential horizontal ambiguity: SwitchStmt[#1]: "Switch " Variable SwitchCases <--> S1
 * *** potential horizontal ambiguity: SwitchCases[#1]: SwitchCase <--> SwitchCases */
////////////////////////////////////////////////////////////////////////////////
Arithmetic          : "(" Arithmetic ")"
                    | Variable A
                    | LeftUnOperator Variable
A                   : BinOperator Variable
                    | UnOperator
Assignment          : NestedId AssignMode                                       //******** Ændret
AssignMode          : AssignOperator RightAssign                                //******** Ændret
                    | "(" Id MultiAssign Variable ")"                           //******** Ændret

//Assignment          : NestedId AssignOperator RightAssign
//                    | NestedId "(" Id MultiAssign Variable ")"
MultiAssign         : ", " Id MultiAssign Variable ", "
                    | ") = ("
AssignOperator      : " = "
                    | " += "
                    | " -= "
                    | " %= "
                    | " *= "
                    | " /= "
RightAssign         : Arithmetic
                    | Variable
                    | FunctionCall
FunctionCall        : NestedId "(" Arguments ")"
                    | NestedId "()"
Arguments           : Variable ", " Arguments
                    | Arithmetic ", " Arguments
                    | Variable
                    | Arithmetic
LoopStmt            : WhileStmt
                    | ForStmt
                    | ForeachStmt
WhileStmt           : "While " Arithmetic " Do " Stmts " End While"
ForeachStmt         : "Foreach " TypeId " " Id " In " NestedId " Do " Stmts " End Foreach"    // Foreach Unit unit In units.unitList Do
ForStmt             : "For " Integer ForIterator Integer " Do " Stmts " End For"
ForIterator         : " To "
                    | " DownTo "
BinOperator         : " * "
                    | " + "
                    | " / "
                    | " - "
                    | " % "
                    | LogicOperator
                    | RelaOperator
LogicOperator       : " AND "
                    | " OR "
RelaOperator        : " EQUALS "
                    | " GREATERTHAN "
                    | " LESSTHAN "
                    | " GREATERTHANEQUALS "
                    | " LESSERTHANEQUALS "
UnOperator          : "++"
                    | "--"
LeftUnOperator      : " NOT"
                    | " -"
                    | UnOperator
Variable            : NestedId
                    | Number
                    | String
                    | Boolean
Dcl                 : "Declare" Id Dcls " as " TypeId    // Declare units as Unit[90]
Dcls                : ", " Id Dcls
                    |
TypeId              : Id ArrayDcl                                               //****** Ændret
                    | "Number" ArrayDcl                                         //****** Ændret
                    | "String" ArrayDcl                                         //****** Ændret
                    | "Boolean" ArrayDcl                                        //****** Ændret
                    | "Void"                                                    //****** Ændret
                    | "List<" TypeId ">"            // List<Unit>
ArrayDcl            : "[" Integer "]"                                           //****** Ændret
                    |                                                           //****** Ændret
// Follow set: "id", >, If, While, Foreach, For, Switch, Return, End, Default, Function
//ListDcl             :
//                    | TypeId "[" Integer "]"
NestedId            : Id N
N                   : "." NestedId
                    | "[" Integer "]" N
                    |
Id                  : "id"
Number              : "num"
String              : "str"
Boolean             : "bool"
Integer             : "int"

////////////////////////////////////////////////////////////////////////////////
/* Regular Expressions - testet på https://regex101.com/   // Nedenstående er keywords i sproget
 * Id                  : ^(?=[a-zA-Z_]+[a-zA-Z0-9_]*)(?!.*(AND|OR|NOT|EQUALS|GREATERTHAN|GREATERTHANEQUALS|LESSTHAN|LESSTHANEQUALS|If|While|Do|Foreach|Function|End|Begin|Program|For|Type|as|Number|String|Boolean|Declare|In|Return||Else||List|Include|Switch|Default|Case|NULL|Define|Void)).+$

 * Number              : ^[+-]?[0-9]*\.?[0-9]+$
 * String              : ^"([^"\\]*(\\.[^"\\]*)*)"$
 * Boolean             : ^(true|false)$
 * Integer             : ^[0-9]+$
 * RegEx'en virker ikke med https://services.brics.dk/java/grammar/demo.html
 * hvor der er testet for ambiguity.                                          */
////////////////////////////////////////////////////////////////////////////////
